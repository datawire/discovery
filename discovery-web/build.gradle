apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

ext {
  guavaVersion = '19.0'

  // Quark dependencies should be specified as below. The $group, $name, and $version are the values used to
  // resolve from Maven. The $src is the URL or file path to a Quark source file.
  quarkDependencies = [
      [
          group   : 'datawire_mdk',
          name    : 'datawire_mdk',
          version : '2.0.0',
          src     : 'https://raw.githubusercontent.com/datawire/mdk/master/quark/mdk-2.0.q'
      ],
  ]
}

task quarkInstall() { }

tasks.whenTaskAdded { t ->
  if(t.name.equals('build')){
    t.dependsOn quarkInstall
  }
}

ext.quarkDependencies.each { depInfo ->
  def depName = "${depInfo.name}-${depInfo.version}".toString()

  task "quarkInstall_${depName}"(type: Exec) {
    executable 'quark'
    args       'install', '--java', depInfo.src
  }

  quarkInstall.dependsOn "quarkInstall_${depName}"
  project.dependencies.add('compile', "${depInfo.group}:${depInfo.name}:${depInfo.version}")
}

dependencies {
  compile group: "ch.qos.logback", name: "logback-classic", version: parent.ext.logbackVersion

  compile group: 'com.google.guava', name: 'guava', version: guavaVersion

  compile group: "io.vertx", name: "vertx-auth-jwt", version: parent.ext.vertxVersion
  compile group: "io.vertx", name: "vertx-core",     version: parent.ext.vertxVersion
  compile group: "io.vertx", name: "vertx-web",      version: parent.ext.vertxVersion
  
  testCompile group: "io.vertx", name: "vertx-unit", version: parent.ext.vertxVersion
}

mainClassName = 'io.vertx.core.Launcher'

applicationDistribution.from(parent.projectDir) {
  include 'LICENSE'
  include 'NOTICE'
  include 'README.md'
  into ''
}

shadowJar {
  classifier = 'fat'
  
  manifest {
    attributes 'Main-Verticle': "${project.group}.ServiceVerticle"
  }
  
  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}